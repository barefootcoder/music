#! /usr/bin/env perl

use myperl DEBUG => 1;
use autodie qw< :all >;

use Getopt::Std;
use File::Basename;
use File::Stat qw< stat >;

use Music;


my $opts = {};
getopts('hbn:l:xB:v', $opts);
$opts->{n} //= $opts->{l};

HELP_MESSAGE() if $opts->{h};

sub HELP_MESSAGE
{
	say STDERR "usage: $ME [-h] | [-b] [-n num | -x num] [-B file]";
	say STDERR "           -b : bare names only";
	say STDERR "           -n : show at least num recent albums";
	say STDERR "           -l : alias for -n";
	say STDERR "           -x : show exactly num most recent albums";
	say STDERR "           -B : show only albums before file's mod date";
	say STDERR "           -v : be chatty";
	say STDERR "           -h : this help message";
	exit;
}

my $cutoff = $opts->{B} ? stat($opts->{B})->mtime : time;

# yes, we really do mean || and not // here
const our $NUM_ALBUMS => $opts->{x} || $opts->{n} || 10;


my %files;
my $count = 0;
print "Checking albums ..." if $opts->{v};
foreach ( all_albums )
{
	++$count;
	my $time = stat($_)->mtime;
	my $day = time2str('%Y%m%d', $time);
	$files{basename($_)} = { time => $time, day => $day };
}
say " done [$count albums checked]" if $opts->{v};
my @files = sort { $files{$a}->{time} <=> $files{$b}->{time} } grep { $files{$_}->{time} < $cutoff } keys %files;

my $day_of_final = $files{$files[-$NUM_ALBUMS]}->{day};
my $show_year = $day_of_final < time2str("%Y0101", time);
@files = @files[-$NUM_ALBUMS..-1] if $opts->{x};
say '' unless $opts->{b};
$count = 0;
foreach (@files)
{
	my $day = $files{$_}->{day};
	$day =~ /^\d\d(\d\d)(\d\d)(\d\d)/;
	next unless $day >= $day_of_final;
	$day = $show_year ? "$2/$3/$1" : "$2/$3";
	++$count;
	say $opts->{b} ? $_ : "$day  $_";
}
say '' unless $opts->{b};
say "Showed latest $count albums." if $opts->{v};
