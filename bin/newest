#! /usr/bin/env perl

use myperl DEBUG => 1;
use autodie qw< :all >;

use Getopt::Std;
use File::Basename;
use File::Stat qw< stat >;

use Music;


my $opts = {};
getopts('hbn:x:', $opts);

HELP_MESSAGE() if $opts->{h};

sub HELP_MESSAGE
{
	say STDERR "usage: $ME [-h] | [-b] [-n num | -x num]";
	say STDERR "           -b : bare names only";
	say STDERR "           -n : show at least num recent albums";
	say STDERR "           -x : show exactly num most recent albums";
	say STDERR "           -h : this help message";
	exit;
}

# yes, we really do mean || and not // here
const our $NUM_ALBUMS => $opts->{x} || $opts->{n} || 10;


my %files;
foreach ( $ALBUM_DIR->children )
{
	my $time = stat($_)->mtime;
	my $day = time2str('%Y%m%d', $time);
	$files{basename($_)} = { time => $time, day => $day };
}
my @files = sort { $files{$a}->{time} <=> $files{$b}->{time} } keys %files;

my $day_of_final = $files{$files[-$NUM_ALBUMS]}->{day};
@files = @files[-$NUM_ALBUMS..-1] if $opts->{x};
say '' unless $opts->{b};
foreach (@files)
{
	my $day = $files{$_}->{day};
	$day =~ /^\d\d\d\d(\d\d)(\d\d)/;
	say $opts->{b} ? $_ : "$1/$2  $_" if $day >= $day_of_final;
}
say '' unless $opts->{b};
